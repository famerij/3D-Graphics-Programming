<?doctype HTML>
<html>
<head>
  <meta charset="utf-8">
  <script src="./lib/jquery-1.9.1.min.js"></script>
  <script src="./lib/three.js"></script>
  <script src="./lib/d3.v3.js"></script>
  <script src="scene.js"></script>
  <!-- vertex shader for custom lighting -->
  <script id="light-vs" type="x-shader/x-vertex">
    uniform vec3 u_spotLightPos;
    uniform vec3 u_spotLightDir;

    varying vec2 v_uv;
    varying vec3 transformedNormal;
    varying vec4 mvPos;
    varying vec4 worldPos;
    varying vec3 spotLightDir;
    varying vec3 spotLightSpotDir;

    void main() {
      // Eye space normal
      transformedNormal = normalize(normalMatrix * normal);
      // Vertex position in camera space
      mvPos = modelViewMatrix * vec4(position,1);
      // Vertex position in world space
      worldPos = modelMatrix * vec4(position,1);
      //Point light
      vec4 spotLightViewPos = viewMatrix * vec4(u_spotLightPos, 1.0);
      spotLightDir = spotLightViewPos.xyz - mvPos.xyz;
      spotLightSpotDir = normalize(u_spotLightDir);
      // pass texcoords
      v_uv = uv;
      // compute vertex position
      gl_Position = projectionMatrix * mvPos;
    }
  </script>
  <!-- fragment shader for custom lighting -->
  <script id="light-fs" type="x-shader/x-fragment">
    uniform sampler2D map;
    // global ambient
    uniform vec4 u_ambient;

    // our directional light
    uniform vec3 u_dirLightPos;
    uniform vec4 u_dirLightColor;
    // spotlight
    uniform vec4 u_spotLightColor;
    uniform float u_spotLightDist;
    uniform float u_spotLightInt;
    uniform float u_spotLightExp;
    uniform float u_spotLightAngle;

    //varying vec4 v_color;
    varying vec2 v_uv;
    varying vec3 transformedNormal;
    varying vec4 mvPos;
    varying vec4 worldPos;
    varying vec3 spotLightDir;
    varying vec3 spotLightSpotDir;

    void main(){
      vec3 n = normalize(transformedNormal);
      // Normalize directional light's direction
      vec4 lightViewDir = viewMatrix * vec4(u_dirLightPos, 0.0);
      vec3 lightDir = normalize(lightViewDir.xyz);
      // Cosine between normal and light direction (diffuse light amount)
      float diffuseLightAmount = max(dot( n, lightDir ), 0.0);

      vec3 v_light = u_ambient.xyz + diffuseLightAmount * u_dirLightColor.xyz;

      // Dot product between normal and spotlight direction from fragment
      float NdotL = max(dot(n, normalize(spotLightDir)), 0.0);

      //Check if normal points towards spotlight
      if (NdotL > 0.0) {
        float spotEffect = dot(spotLightSpotDir, normalize(-spotLightDir));
        if (spotEffect > u_spotLightAngle) {
            spotEffect = pow(spotEffect, u_spotLightExp);

            float spotDistance = 1.0;
            if (u_spotLightDist > 0.0) {
              spotDistance = 1.0 - min( ( length( spotLightDir ) / u_spotLightDist ), 1.0 );
            }

            v_light += u_spotLightColor.xyz * NdotL * spotDistance * spotEffect * u_spotLightInt;
        }
      }

      // combine texel color with received color from vertex shader
      gl_FragColor = texture2D(map, v_uv) * vec4(v_light, 1.0);
    }
  </script>
</head>
<body>
<div id="main" style="border: solid 1px;" width="800" height="600"></div>
<div id="fps" style="position:absolute;width:100px;height:50px;top:10px; left:10px;"></div>
</body>
</html>
